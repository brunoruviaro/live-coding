(
Ndef(\a, { var snd;
	var env;
	var detune = Array.rand(8, 0.99, 1.01);
	snd = SinOsc.ar([50, 110, 220, 330, 441] * detune) * Ndef(\h).kr;
	snd = snd.clip(-1, 1);
	snd = snd * SinOsc.ar(1/[2, 3, 4]).range(0.05, 0.1);
	
	snd = (snd.distort * 10).clip(-1, 1) * 0.05;
	snd = RLPF.ar(
		in: snd, 
		freq: LFTri.ar(1/[5, 7, 3]).range(300, 2300),
		rq: 0.3
	);
	env = Env.perc(0.01, Array.rand(8, 1, 4)).kr(gate: Dust.kr([1, 2, 4]));
	snd = snd * env;
	snd = Splay.ar(snd);
	});
)

Ndef(\h, { LFNoise0.kr(3).range(2, 16).round(1) })

Ndef(\a).fadeTime = 5;
Ndef(\a).play;

// follow-up 2021-04-27:

Ndef(\h, { LFNoise0.kr(1).range(2, 16).round(1) });

(
Ndef(\a, { var snd;
	var env;
	var detune = Array.rand(8, 0.99, 1.01);
	snd = SinOsc.ar([50, 110, 220, 330, 441] * detune * LFPulse.ar(1/5).range(1, 3.midiratio)) * Ndef(\h).kr;
	snd = snd.clip(-1, 1);
	snd = snd * [SinOsc, LFSaw].choose.ar(1/[2, 3, 4, 0.5]).range(0.05, 0.31);
	
	snd = (snd.distort * 10).clip(-1, 1) * 0.1;
	snd = snd.fold(-0.9, 0.9);
	snd = RLPF.ar(
		in: snd, 
		freq: LFTri.ar(1/[5, 7, 8]).range(2300, 5300) * [0.5, 1.1, 2.2],
		rq: 0.3
	);
	env = Env.perc(0.01, Array.rand(8, 1, 4)).kr(gate: Dust.kr([1, 2, 4]));
	snd = snd * env;
	snd = Splay.ar(snd);
	snd = (LPF.ar(snd, 400) * SinOsc.ar(1/7).range(0, 4)) + snd;
	snd = snd 
	+ 
	(NHHall.ar(
		in: snd, 
		rt60: 3
	) * 0.5);
	// snd
	snd = Limiter.ar(snd);
	});
)



(
Tdef(\change, {
	loop {
		
		"noisy".postln;
		Ndef(\h, { LFNoise0.kr(1).range(2, 16).round(1) });
		10.wait;
		"less noisy".postln;
		Ndef(\h, { LFNoise0.kr(1).range(0.5, 1).round(1) });
		5.wait;
	}
}).play;
)


Ndef(\a).fadeTime = 5;
Ndef(\a).play;
