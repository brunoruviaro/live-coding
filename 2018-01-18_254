(
SynthDef(\birds, { arg freq,amp,dur,ar,att,rel,gliss;
	var snd, env;
	ar = [att,rel].normalizeSum * dur;
	env = Env.perc(ar[0], ar[1], amp).kr(2);
	snd = SinOsc.ar(freq: Line.kr(freq, freq*gliss, dur), mul: env);
	Out.ar(0, snd!2)
}).add
)

(
Pbindef(\bird1,
	\instrument, \birds,
	\dur, Prand([1/4, 0.125, 0.2, Rest(4)], inf),
	\freq, Pseq([440, 1000, Pn(3333, 4)], inf),
	\gliss, Pseq([1.3, 1.1, Pn(0.9, 2)], inf),
	\att, 0.1,
	\rel, 0.9,
	\legato, 1,
	\amp, Pwhite(0.2, 0.5),
)
)
Pbindef(\bird1).play;
Pbindef(\bird2).play;
Pbindef(\bird3).play;

Pbindef(\bird1).clear

(
Pbindef(\bird2,
	\instrument, \birds,
	\freq, 10000,
	\dur, Pxrand([1.4, Rest(4), Rest(2)], inf),
	\gliss, [0.5, 0.51],
	\att, 0.5,
	\rel, 1,
	\legato, 1,
	\amp, Pwhite(0.2, 0.5),
)
)


(
Pbindef(\bird3,
	\instrument, \birds,
	\freq, Pwhite(1000, 9999),
	\dur, Prand([Pn(0.14, 7), Rest(4), Rest(2)], inf),
	\gliss, [0.95, 0.51],
	\att, 0.5,
	\rel, 1,
	\legato, 1,
	\amp, Pwhite(0.2, 0.5),
)
)

(
Pbindef(\bird1,
	\instrument, \birds,
	#[freq, dur], Pseq([
		[1400, 1/6],
		[1405, 1/6],
		[1410, 1/7],
		[1300, 1/4],
		Pn([ 3555, 0.12], 4),
		[\rest, 3]
	] ,inf),
	\gliss, Pwhite(1.5, 2),
	\att, 0.1,
	\detune, [0, 10, 55, 100],
	\rel, 0.9,
	\legato, 1,
	\amp, Prand([
		Pwhite(0.2, 0.5),
		Pseries(0.6, -0.1, 4),
		0.4
	], inf)
)
)


(
Ndef(\fx, {
	var snd = In.ar(0, 2);
	var snd2 = CombN.ar(snd, 1, LFNoise0.kr(1/4).range(0.1, 0.9), 1);
	snd2 = Limiter.ar(snd2 * snd, 0.5);
	snd = snd.distort + (snd2 * 0.5);
	snd = FreeVerb.ar(snd, 0.2, 0.6, 0.4);
	ReplaceOut.ar(0, snd);
})
)


Pbindef.clear(5);
